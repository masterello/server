buildscript {
    ext {
        springBootVersion = '3.1.3'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id('base')
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.masterello'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '20'

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.34'
        annotationProcessor 'org.projectlombok:lombok:1.18.34'

        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-test'


        compileOnly "org.mapstruct:mapstruct:1.5.5.Final"
        annotationProcessor "org.projectlombok:lombok-mapstruct-binding:0.2.0"
        annotationProcessor "org.mapstruct:mapstruct-processor:1.5.5.Final"

        implementation 'io.springfox:springfox-swagger-ui:2.9.2'
        implementation 'io.springfox:springfox-swagger2:2.9.2'
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

        testCompileOnly 'org.projectlombok:lombok:1.18.34'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.34'
    }

    test {
        useJUnitPlatform()
    }
}

tasks.register('createMigration') {
    doLast {
        if (project.hasProperty("migrationName")) {
            String fileContents = new File("$projectDir/db/src/main/resources/db/sample_migration.sql").text

            String migrationName = project.getProperty("migrationName").replaceAll(" ", "_").trim()

            TimeZone.setDefault(TimeZone.getTimeZone('UTC'))
            String dateStamp = new Date().format('yyyyMMdd')
            String timeStamp = new Date().format('HHmmss')
            String fileName = "V" + dateStamp + "_" + timeStamp + "__" + migrationName + ".sql"

            new File("$projectDir/db/src/main/resources/db/migration/1.0.0/", fileName).text = fileContents
        } else {
            throw new GradleException('-PmigrationName is missing')
        }
    }
}

